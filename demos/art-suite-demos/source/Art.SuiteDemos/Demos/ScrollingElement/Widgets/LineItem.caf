import &StandardImport, {} &Button

class LineItem extends FluxComponent

  @stateFields addSize: 100
  @propFields :removeItem :text :horizontal

  expand: -> @addSize += max 50, @addSize
  collapse: -> @addSize =
    max 0, min @addSize / 2, @addSize - 50

  removeItemNow: -> @removeItem @text

  render: ->
    {text, horizontal} = @props
    Element
      :childrenSize :clip
      key:    "" lineItem:#{text}
      margin: 5
      animators:
        size:   toFrom: if horizontal then hch: 1 w: 0 else wcw: 1 h: 0
        margin: toFrom: 0
      draw: vibrant

      Element
        size:
          if horizontal
                cs: 1 w: @addSize
          else  cs: 1 h: @addSize
        animators:          :size
        childrenMargins:    15
        padding:            20
        :childrenTopLeft
        if horizontal then :column else :row

        TextElement
          mediumText
          :childrenSize
          fontSize: 64
          text:     text
          color:    lightMuted

        Button text: :delete  action: @removeItemNow
        Button text: :bigger  action: @expand
        Button text: :smaller action: @collapse
