import &StandardImport,
  &WidgetLib
  {} &ActivityBadge, &Icon

&ArtEngine.Element extract getSizePointLayout

class ToolbarButton extends ViewStateComponent

  @propFields
    :selected

  @getter
    color: ->
      if @selected
        AppPalette.primaryDark1
      else
        @props.color ? TextPalette.black.primary

  renderIcon: ->
    @props extract iconSize, badgeBorder, icon, shadow, iconShadow, iconColor, iconScale = 1, badge, customIcon
    []
      customIcon
      ? icon &&
        Icon
          icon:       icon
          size:       iconSize ? 25 * iconScale
          animators:  :size :fontSize
          color:      iconColor ? @color
          shadow:     iconShadow ? shadow

      badge > 0 && ActivityBadge
        axis: .5
        location: xw: .75, yh: .22
        count: badge
        border: badgeBorder

  preprocessProps: (props) ->
    props extract color, dimText, brightText, lightColor

    color ||= if lightColor
      if brightText
            TextPalette.white.primary
      else  TextPalette.white.secondary
    else
      if dimText
            TextPalette.black.tertiary
      else  TextPalette.black.secondary

    merge props, {} color

  # icon required
  renderIconWithCount: ->
    @props extract count, color

    if count?
      Element
        :row
        :childrenCenterLeft
        :childrenSize
        childrenMargins: @margin
        count > 0 && TextElement
          TextStyles.smallText
          :childrenSize
          animators: size: toFromVoid: hch:1, w:0
          clip:   true
          color:  color
          text:   formatted3DigitCount count

        @renderIcon()

    else
      @renderIcon()

  @getter
    margin: -> @gridSize / 2

  render: ->
    @props extract
      palette, maxLines, iconRight, childrenMargins, shadow, labelStyle
      textPadding, innerPadding, size, hoverAction, align
      maxLargeLabelLength = 8
      tight
      action, disabled, selected, text, label, lightColor, count, badge, labelBottom

    @ extract gridSize, color
    margin = gridSize / 2

    label ||= text

    if tight
      size = if labelBottom
        @buttonSize
      else
        :childrenWidthParentHeight

    size = getSizePointLayout size if size

    AppKitButtonWrapper
      if @props.children
        objectWithout @props, :children
      else @props
      {}
        size
        drawPadding:  2
        draw: drawButton

        hoverAction:  hoverAction

      Element
        if labelBottom then :column else :row
        size: if size?.childrenRelative then size else :parentSize
        padding: innerPadding ?
          if !labelBottom || maxLines?
            if label
              h: gridSize / 2
            else
              h: margin / 2
        childrenAlignment:  align || :centerCenter
        childrenMargins:    childrenMargins ?
          if labelBottom
            TextStyles.smallText.fontSize / 4
          else @gridSize / 2

        @props.children

        @renderIconWithCount() unless iconRight

        label && Element
          if labelBottom then :parentWidthChildrenHeight
          else :childrenSizeMaxParentWidth

          TextElement labelStyle ? (if labelBottom then TextStyles.tinyText else TextStyles.smallText),
            maxLines: maxLines
            if labelBottom
              if maxLines
                size:   :parentWidthChildrenHeight
                align:  :centerCenter
              else
                size:     :childrenSize
                axis:     :topCenter
                location: :topCenter

            else
              size: cs: 1, max: ps: 1.5
              maxLines: 2
            scale: (ps, cs) -> min 1, ps.minRatio cs

            padding:  textPadding
            text:     label
            color:    color
            shadow:   shadow

        @renderIconWithCount() if iconRight
