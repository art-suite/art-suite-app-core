import &StandardImport

class CanvasPathSet extends BaseClass
  constructor: (a)->
    if a instanceof XbdTag
      @_initFromXbd a
    else
      @paths = if isArray a
        a
      else if a?
        throw new Error "unsupported type constructing CanvasPathSet: #{formattedInspect a}"
      else
        []

  @property :aspectRatio

  _initFromXbd: (xbdTag) ->
    @paths = array pathTag in xbdTag.tags
      new &CanvasPath pathTag

  applyPath: (context, size) ->
    each path in @paths
      path.applyPath context, size

  @getter
    xbd: ->
      CanvasPathSetTag
        {@aspectRatio} if @aspectRatio?
        array path in @paths with path.xbd

    inspectedObjects: ->
      CanvasPathSet: toInspectedObjects @paths

    drawArea: ->
      reduce area, p in @paths inject rect()
        p.drawArea.unionInto area

  normalize: (drawArea = @drawArea) ->
    each path in @paths
      path.normalize drawArea

    @aspectRatio = drawArea.aspectRatio
    @

  removeStyles: ->
    each path in @paths
      path.removeStyles()
