import &ArtStandardLib
require(:art-engine) extract
  Layout.PointLayout.pointLayout
  Core.NamedElementPropValues.namedSizeLayouts
&ArtAtomic extract Point.namedAlignmentPoints

merge
  object v in namedSizeLayouts
    size: v

  object v, k in namedAlignmentPoints
    location: pointLayout k
    axis: v

  # Ex: childrenCenterCenter >> childrenAlignment: :centerCenter
  object v, k in namedAlignmentPoints with-key lowerCamelCase "" children #{k}
    childrenAlignment: v

  # Ex: textCenterCenter >> align: :centerCenter
  object v, k in namedAlignmentPoints with-key lowerCamelCase "" text #{k}
    align: v

  {}
    row:    childrenLayout: :row
    column: childrenLayout: :column
    flow:   childrenLayout: :flow
    bold:   fontWeight: :bold

    fitParent =
      scale:    (ps, cs) -> ps.minRatio cs
      location: :centerCenter
      axis:     :centerCenter


    fillParent =
      scale: (ps, cs) -> ps.maxRatio cs
      location: :centerCenter
      axis:     :centerCenter

    fitIntoParent:  fitParent
    zoomIntoParent: fillParent

  cursors = w
    ""
      default pointer
      allScroll contextMenu help
      wait progress

      crosshair cell text verticalText

      alias copy move noDrop notAllowed
      zoomIn zoomOut

      grab grabbing

      eResize nResize neResize nwResize sResize seResize swResize wResizee wResize nsResize neswResize nwseResize colResize rowResize

  object cursor in cursors with-key "" #{cursor}Cursor
    cursor: dashCase cursor