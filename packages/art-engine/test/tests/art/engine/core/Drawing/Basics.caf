import &ArtStandardLib, &ArtEngine

{testDownsampledRedChannelEq} = &CoreHelper

suite: ->
  testDownsampledRedChannelEq
    "drawing rectangles"
    new Element
      size: 4
      new RectangleElement color: #000, size: 4
      new RectangleElement color: #fff, location: 1, size: 3
    compactFlatten []
      0 0 0 0
      0 7 7 7
      0 7 7 7
      0 7 7 7

  testDownsampledRedChannelEq
    "unconstrained drawing"
    new Element
      size: 2, location: 1
      new RectangleElement size: 2, color: "#700"
      new RectangleElement size: 2, location: 1, color: "#f00"

    compactFlatten []
      3 3 0
      3 7 7
      0 7 7

  testDownsampledRedChannelEq
    "aligned rectangle mask drawing"
    new Element
      size: 2, location: 1
      new RectangleElement size: 2,   color:  #700
      new RectangleElement size: 10,  color:  #f00, location: 1
      new RectangleElement size: 2,   isMask: true

    compactFlatten []
      3 3
      3 7

  testDownsampledRedChannelEq
    "rotated rectangle"
    new Element
      size: 6
      new RectangleElement color: :black
      new Element
        location: ps: .5
        size:     ps: 2/3
        axis: .5
        angle: Math.PI/4
        layoutPixelSnap: false
        new RectangleElement color: #700
        new RectangleElement
          color:    #f00
          location: ps: .5
          size:     ps: .75
        new RectangleElement isMask: true

    compactFlatten []
      0 0 1 1 0 0
      0 1 3 3 1 0
      1 3 3 3 3 1
      1 3 5 5 3 1
      0 1 7 7 1 0
      0 0 2 2 0 0