import &StandardImport

class App extends FluxComponent
  @subscriptions :paginator.paginator :obiWan.location

  render: ->
    {pageDownEnabled, pageUpEnabled, loadDirection, startDarkJediId, loadedSlots} = @paginator

    find darkJedi in loadedSlots when darkJedi?.homeworld.id == @location.id
      pageUpEnabled = pageDownEnabled = false

    Element
      size:       ww: 1 hch: 1 max: w: 800
      axis:       :centerCenter
      location:   :centerCenter
      draw:       appOutlineStyle

      Element
        size:             :parentWidthChildrenHeight
        childrenLayout:   :column
        padding:          basePadding
        childrenMargins:  basePadding

        &PlanetMonitor {} @location

        Element
          size:             :parentWidthChildrenHeight
          childrenLayout:   :row
          childrenMargins:  basePadding

          Element
            size:             :parentWidthChildrenHeight
            childrenLayout:   :column
            childrenMargins:  basePadding
            array darkJedi in loadedSlots
              &Slot {} darkJedi, alert: darkJedi?.homeworld.id == @location?.id

          Element
            size:             w: 100 hh: 1
            childrenLayout:   :column
            childrenMargins:  basePadding
            &Button action: pageUpEnabled    && @models.paginator.pageUp, up: true
            &Button action: pageDownEnabled  && @models.paginator.pageDown
