import &ArtSuite # 2 tokens         >> 226 total tokens

textStyle =                         # 10 tokens
  fontFamily: "" "Century Gothic", Futura, sans-serif
  fontSize:   14
  margin:     10
  size:       :childrenSize                         # -1 for upcoming ArtEngine keyword props

calculateWinner = (board) -> # 59
  find line in []
      0 1 2
      3 4 5
      6 7 8
      0 3 6
      1 4 7
      2 5 8
      0 4 8
      2 4 6
    [a, b, c] = array el in line    # -1: upcoming-dot-backref: array line with board[.]
      board[el]
    a == b && a == c && a                           # -2 less when we have: a == b == c

class Square extends Component      # 28 tokens
  render: ->
    Element
      size: 35
      draw: outline:      :black
      on:   pointerClick: @props.play
      TextElement
        textStyle
        fontWeight: :bold
        fontSize:   18
        size:       :parentSize                     # -3 for upcoming ArtEngine keyword props
        align:      :centerCenter
        @props

class TicTacToe extends Component     # 127 tokens
  @stateFields turnNumber: 0 history: [] array til 9 with null

  play: (player, boardIndex) ->       # 28 tokens
    @history = arrayWith
      @history.slice 0, @turnNumber + 1   # -3 when we have: array in @history to @turnNumber
      arrayWithElementReplaced @history[@turnNumber++], player, boardIndex # -2 - replace ',' with new-lines

  render: ->                          # 84 tokens
    winner = calculateWinner board = @history[@turnNumber] # -1 with: @getter board: -> @history[@turnNumber]
    player = if @turnNumber % 2 then :X else :O
    Element
      size:             w: 125
      padding:          10
      childrenLayout:   :flow                       # -1 for upcoming ArtEngine keyword props

      array text, boardIndex in board
        Square {} text, play: unless winner || text # -2 -- ',' with new-lines
          -> @play player, boardIndex

      TextElement
        textStyle
        text:
          if winner then  "Winner: "      + winner
          else            "Next player: " + player

      array i til @history.length
        TextElement
          textStyle
          on: pointerClick: -> @turnNumber = i
          text:
            if i then     "Go to move #" + i
            else          "Go to game start"
