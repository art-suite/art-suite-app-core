import &StandardImport

class App extends FluxComponent
  @subscriptions :paginator.paginator :obiWan.location

  render: ->
    {pageDownEnabled, pageUpEnabled, loadDirection, startDarkJediId, loadedSlots} = @paginator

    if find darkJedi in loadedSlots when darkJedi?.homeworld.id == @location.id
      pageUpEnabled = pageDownEnabled = false

    Element
      size: ww: 1 hch: 1 max: w: 800
      axis: :centerCenter
      location: ps: :centerCenter

      RectangleElement
        radius: 2
        OutlineElement appOutlineStyle

      Element
        size: ww: 1 hch: 1
        childrenLayout: :column
        padding: basePadding

        &PlanetMonitor {} @location

        Element
          size: ww: 1 hch: 1
          childrenLayout: :row

          Element
            size: ww: 1 hch: 1
            childrenLayout: :column
            margin: basePadding
            array darkJedi in loadedSlots
              &Slot {} darkJedi, alert: darkJedi?.homeworld.id == @location?.id

          Element
            size: w: 100 hh: 1
            childrenLayout: :column
            &Button action: pageUpEnabled    && @models.paginator.pageUp, up: true
            &Button action: pageDownEnabled  && @models.paginator.pageDown
