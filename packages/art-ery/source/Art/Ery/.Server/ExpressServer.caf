import &ArtStandardLib, &ArtCommunicationStatus

class ExpressServer extends &LoggingMixin &ArtClassSystem.BaseClass

  @defaults:
    port:   8085
    server: :http://localhost

  @start: (manyOptions...) ->
    new ExpressServer
    .start manyOptions...

  start: (manyOptions...) ->
    {numWorkers} = @options = merge ExpressServer.defaults, manyOptions...

    @logVerbose start: {}
      env:      object v, k from process.env when k.match /^art/
      Neptune: Neptune.getVersions()
      @options

    if numWorkers > 1
      @log start: throng: workers: numWorkers
      &throng
        start:    fastBind @_startOneServer, @
        workers:  numWorkers
        lifetime: Infinity
    else
      @_startOneServer()

  ##################
  # PRIVATE
  ##################
  _startOneServer: ->

    ##
      IN: options:
        port:     (number) port to listen on
        name:     (string) just for informational purposes
        static:   (string) path to static assets to serve
        handlers: [middleware: ->] - array objects with 'middleware' properties to be passed to express.use

    {port, name = 'Art.Ery.ExpressServer', handlers} = @options
    staticOptions = @options.static

    app = &express()
    app.use &compression()

    each handler in if isArray handlers then handlers else [handlers]
      app.use handler.getMiddleware @options

    if staticOptions
      app.use &express.static staticOptions.root, merge
        staticOptions
        maxAge:     3600*24*7 # 1 week
        setHeaders: (res) -> res.setHeader :Access-Control-Allow-Origin :* # CORS

    app.listen port, =>
      @log "" listening on: http://localhost:#{port}
